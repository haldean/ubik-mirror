/*
 * token.l: expel language tokenizer
 * Copyright (C) 2016, Haldean Brown
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along
 * with this program; if not, write to the Free Software Foundation, Inc.,
 * 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
 */

%{

#include "expel/token.h"
#include "grammar.tab.h"

/* these are generated by generated code, and they're innocuous, so we
 * suppress them. */
#pragma GCC diagnostic ignored "-Wunused-parameter"

#define TOKEN(x) { yylval->token = x; return x; }
#define STRING(token) { \
        yylval->string = strdup(yytext); \
        return token; \
}

char *read_string_literal(yyscan_t scanner);

%}

%option reentrant
%option noyywrap
%option yylineno
%option nounput
%option bison-bridge

NAME [a-z_][a-zA-Z0-9/_\-']*

%%

\n
[ ]

#.*                       /* swallow comments */

"!"                       TOKEN(IMMEDIATE);
":"                       TOKEN(BIND)
"^"                       TOKEN(TYPE)
"=>"                      TOKEN(IMPLIES)
"->"                      TOKEN(GOES_TO)
"\\"                      TOKEN(LAMBDA)
"="                       TOKEN(IS)
"("                       TOKEN(OPEN_PAR)
")"                       TOKEN(CLOSE_PAR)
"+"                       TOKEN(USES)
"\."                      TOKEN(MEMBER)
[0-9]+                    { yylval->integer = atoi(yytext); return INTEGER; }
([0-9]+\.[0-9]*|\.[0-9]+) { yylval->floating = atof(yytext); return NUMBER; }
{NAME}                    STRING(NAME)
{NAME}:{NAME}             STRING(QUALIFIED_NAME)
[A-Z][a-zA-Z0-9/']*       STRING(TYPE_NAME)
"\""                      { yylval->string = read_string_literal(yyscanner);
                            return STRING; }

.                         { printf("%d: invalid character %s\n",
                                   yylineno, yytext);
                            return yytext[0]; }


%%

char *
read_string_literal(yyscan_t scanner)
{
        char *res;
        char *temp;
        char last;
        char this;
        size_t n;
        size_t cap;

        n = 0;
        cap = 32;
        res = calloc(cap, sizeof(char));
        last = '\0';
        this = '\0';

        for (;;)
        {
                this = input(scanner);
                if (this == '"' && last != '\\')
                        break;

                if (last == '\\')
                {
                        if (this == 'n')
                        {
                                res[n - 1] = '\n';
                                last = this;
                                continue;
                        }
                        if (this == 'r')
                        {
                                res[n - 1] = '\r';
                                last = this;
                                continue;
                        }
                        if (this == 't')
                        {
                                res[n - 1] = '\t';
                                last = this;
                                continue;
                        }
                }

                if (n == cap)
                {
                        cap *= 2;
                        temp = realloc(res, cap);
                        if (temp == NULL)
                        {
                                free(res);
                                return NULL;
                        }
                        bzero(&temp[n], cap - n);
                        res = temp;
                }
                res[n++] = this;
                last = this;
        }

        temp = realloc(res, n + 1);
        if (temp == NULL)
        {
                free(res);
                return NULL;
        }
        return temp;
}

no_ignore xl_error
xl_tokenize(xl_token_cb callback, struct xl_stream *stream)
{
        struct xl_token token;
        yyscan_t scanner;
        xl_error err;
        int ret;
        YYSTYPE token_info;

        err = OK;

        ret = yylex_init(&scanner);
        if (ret != 0)
                return xl_raise(ERR_UNEXPECTED_FAILURE, "yylex_init failed");

        yyset_in(xl_stream_fp(stream), scanner);

        while ((token.token_code = yylex(&token_info, scanner)) > 0)
        {
                token.text = yyget_text(scanner);
                token.text_len = yyget_leng(scanner);
                token.line_no = yyget_lineno(scanner);
                err = callback(token);
                if (err != OK)
                        goto exit;
        }

exit:
        yylex_destroy(scanner);
        return err;
}
